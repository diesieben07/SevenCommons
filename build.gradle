buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'

version = "0.1.4"
def ENV = System.getenv()
if (ENV.DRONE_BUILD_NUMBER) {
	version += "." + ENV.DRONE_BUILD_NUMBER
} else {
	version += ".local"
}

group= "de.take_weiland.mods.commons" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "SevenCommons"

minecraft {
	version = "1.6.4-9.11.1.964"
}

jar {
	manifest {
        attributes 'FMLCorePlugin': 'de.take_weiland.mods.commons.internal.SevenCommons'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

tasks.build.dependsOn('sourceJar', 'deobfJar')

processResources
{
    from (sourceSets.main.resources.srcDirs)
}


artifacts {
	archives deobfJar 
	archives sourceJar
	archives jar
}

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

if (project.hasProperty("mavenUser")) {
	uploadArchives {
		repositories.mavenDeployer {
			configuration = configurations.deployerJars
			repository(url: mavenURL) {
				authentication(userName: mavenUser, password: mavenPassword)
			}
		}
	}
}